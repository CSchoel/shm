within SHM.SeidelThesis.Components;
model SinusNode "Sinus node"
  parameter Real T_0 = 0.9 "heart period for denervated heart";
  parameter Real k_sNe = 1.2 "sensitivity of sinus node to concentration of Norepinephrine";
  parameter Real k_sAc = 0.2 "sensitivity of sinus node to concentration of Acetylcholine";
  Real phase(start=0,fixed=true) "heart cycle phase that rises from 0 to 1 (heartbeat generated) and is then reset to 0 again";
  SHM.Shared.Connectors.SubstanceConcentration sNe "postsynaptic concentration of Norepinephrine";
  SHM.Shared.Connectors.SubstanceConcentration sAc "postsynaptic concentration of Acetylcholine";
  SHM.Shared.Connectors.DiscreteSignal signal "output signal generated by the sinus node";
protected
  parameter Real pemean = SHM.SeidelThesis.Functions.PEMean(100) "mean of the phase effectiveness function over the interval [0,1]";
  Boolean signal0(start=false,fixed=true) "base signal of the sinus node";
equation
  der(phase) = 1/T_0 * (1 + k_sNe * sNe.concentration - k_sAc * sAc.concentration * SHM.SeidelThesis.Functions.PhaseEffectiveness(phase)/pemean);
  sNe.rate = 0;
  sAc.rate = 0;
  when phase > 1 then
    reinit(phase,0);
    signal0 = not pre(signal0);
  end when;
  signal.s = if change(signal0) then 1 else 0;
annotation(Documentation(info="<html>
  <p>Models the sinus node as an integrate-and-fire model with postsynaptic concentrations of Norepinephrine and Acetylcholine as input.</p>
  <p>The effect of Acetylcholine is additionally modulated by a phase effectiveness curve.</p>
</html>"));
end SinusNode;